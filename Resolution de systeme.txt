Ce code n'est valide que pour les Casio

=== Code ===

[[1,0,0][0,1,0][0,0,1->Mat P 'Creation de la matrice pivot
0->C 'Creation du compteur
Dim Mat A
List Ans[1->D 'Nombre d inconnues
Mat A->Mat B 'Recopie de Mat A dans Mat B
Mat B# 'Affichage de Mat B
"DESCENTE"#
For 1->K To D-1
Mat B[K,K->P 'P prend la valeur pivot
If P 'Si P non nul
Then For K+1->I To D
Mat B[I,K->V 'V prend la valeur a annuler
For K->J To D+1
Mat B[I,J]-V/P*Mat B[K,J->Mat B[I,J 'On remplace la valeur en position I,J
Next
Next
Else "PIVOT NUL"
If K<D 'Si on n'est pas a la derniere ligne
Then Mat P->Mat Q
Mat B->Mat Z
For K+1->N To D
If Mat Z[K,K]!=Mat Z[N,K 'Si le nouveau pivot n'est pas nul
Then For 1->M To D+1 'Pivotement des lignes
Mat Z[N,M->Mat B[K,M
Mat Z[K,M->Mat B[N,M
Next
For 1->M To D
Mat Q[N,M->Mat P[K,M
Mat Q[K,M->Mat P[N,M
Next
Dsz K 'K se decremente
C 'Ne pas chercher a comprendre, juste le mettre XD
Isz C 'On incremente le compteur
"MAT PIVOT"#
Mat P#
Break 'On arrete la boucle
IfEnd
Next
IfEnd
IfEnd
Mat B# 'Affichage de la matrice 
Next
"REMONTEE"#
For D->I To 2 Step -1 'I se decremente de 1 a chaque tour
Mat B[I,I->P 'P prend la valeur pivot
If P 'Si P non nul
Then For 1->K To I-1
Mat B[K,I->V
For I->J To D+1
Mat B[K,J]-V/P*Mat B[I,J->Mat B[K,J
Next
Next
Else "PIVOT NUL"
Stop 'Arrêt du programme
IfEnd
Mat B# 'Affichage de la matrice
Next
For 1->I To D 'Calcul des solutions
Mat B[I,D+1]/Mat B[I,I->Mat B[I, D+1
1->Mat B[I, I
Next
"RES"#
Mat B# 'Affichage des solutions
"MAT PIVOT"#
Mat P#
ClrMat Q
ClrMat Z
"CPT="
C#

=== Pour ecrire le code ===

* Menu PRGM puis F3 puis choisir un nom puis EXE

- Le symbole # represente le symbole triangle accessible via SHIFT VARS F5. Il permet d'attendre que l'utilisateur appuie sur EXE pour continuer
- Le symbole ' permet l'écriture de commentaires (non lus par l'interpreteur) et n'est donc pas necessaire a l'execution du code. Il est accessible via F6 puis sixieme caractere sur la droite
- Mat est accessible via OPTN F2 F1
- Dim est accessible via OPTN F2 F6 F2 
- List est accessible via OPTN F1
- If est accessible via SHIFT VARS F1
- For est accessible via SHIFT VARS F6 F1
- Stop est accessible vai SHIFT VARS F2 F4
- Le symbole / est le symbole diviser
- Mat B[I,J equivaut a Mat B[I,J]
- Isz est accessible via SHIFT VARS F3 F4
- ClrMat est accessible via SHIFT VARS F6 F1 F4
- Les symboles != representent le symbole different accessible via SHIFT VARS F6 F3 F2

=== A propos ===

Resout un systeme d'equations a plusieurs inconnues et affiche les differentes etapes.
Change l'ordre des equations si un pivot est nul pendant la descente. La matrice de pivot est affichee ainsi que le compteur. 

=== Utilisation ===

Le code utilise la matrice A qui doit etre editee dans le menu RUN MAT. Appuyer sur F3 puis entrer les dimensions (m=nombre d'inconnues et n=nombre d'inconnues+1) puis EXE. EXIT pour finir.

Exemple mat A :
3 -7 -2 -7
-3 5 1 5
6 -4 0 2

Ensuite aller dans le menu PRGM. Se placer sur le programme et appuyer sur F1.
S'affiche tout d'abord la matrice qui va etre travaillee. Appuyer sur EXE pour passer a la suite a chaque fois. S'affichent ensuite les differentes etapes  pour la triangularisation. La derniere matrice qui s'affiche est le resultat. Celle-ci est visible dans la matrice B depuis RUN MAT. La matrice de pivot est enregistree dans la matrice P.
 
ATTENTION : Le programme utilise les matrices B, P, Q et Z . Leur precedent contenu sera donc efface.